Actor Q2Phalanx : Q2Weapon
{
	Tag "Phalanx Particle Cannon"
	Inventory.PickupMessage "Phalanx Particle Cannon"
	Weapon.AmmoGive 10
	Weapon.SlotNumber 9
	Weapon.AmmoType Magslug
	Weapon.AmmoUse 1
	Inventory.Icon "W_PHLNX"
	States
	{
	Spawn:
		PLAS A -1
		Stop
	Raise:
		TNT1 A 0 A_TakeInventory(WeaponIndex)
		TNT1 A 0 A_GiveInventory(WeaponIndex,12)
		PHA0 ABCDEF 4
		Goto Ready+1
	Ready:
		TNT1 A 0
		PHA1 AAA 6 A_WeaponReady
		TNT1 A 0 A_Jump(64,1)
		Loop
		PHA1 "ABCDEFGHIJKLMNOPQRSTUVWXYZ\[]" 6 A_WeaponReady
		PHA2 ABCDEFGHIJ 6 A_WeaponReady
		Goto Ready+1
	Deselect:
		PHA1 A 3
		PHA0 TUVWX 3
		Goto InstantDeselect
	Fire:
		PHA0 G 4
		PHA0 H 0 A_JumpIfInventory(PowerSilencer,1,4)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Weapons/PhalanxShot",CHAN_WEAPON)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("Weapons/PhalanxShot",CHAN_WEAPON,0.5,0,3)
		TNT1 A 0 A_FireCustomMissile(PhalanxPhoton,0,1,8,-4)
		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_GunFlash
		PHA0 H 4
		PHA0 I 0 A_FireCustomMissile(PhalanxPhoton,0,0,8,-4)
		TNT1 A 0 A_FireCustomMissile(WeaponFlash,0,0)
		TNT1 A 0 A_GunFlash
		PHA0 IJKLMNOPQRS 4
		Goto Ready+1
	Flash:
		TNT1 A 0 A_Light2
		TNT1 A 1 A_SetPitch(Pitch-3.0)
		TNT1 A 1 A_SetPitch(Pitch+1)
		TNT1 A 0 A_Light1
		TNT1 AA 1 A_SetPitch(Pitch+1)
		Goto LightDone
	}
}
Actor PhalanxPhoton : PlasmaBall
{
	RenderStyle Normal
	Damage 10
	ExplosionDamage 64
	Obituary "%o was absolutely destroyed by %k's Phalanx."
	Scale 0.85
	SeeSound ""
	DeathSound "Weapons/RocketExplosion"
	Decal Scorch
	States
	{
	Spawn:
		PHOT AAAABBBBCCCC 1 Bright Light(Rocket) A_SpawnItem(PhalanxPhotonTrailSpawner,0,8)
		Loop
	Death:
		TNT1 A 0 A_Explode
		TNT1 A 5 A_SpawnItem(Q2Explosion)
		Stop
	}
}
Actor PhalanxPhotonTrailSpawner : EffectBase
{
	States
	{
	Spawn:
		TNT1 AAAAAAAA 0 A_SpawnItemEx(BlasterTrail,FRandom(0,5),0,FRandom(-5,5),FRandom(0,0.15),0,FRandom(-0.15,0.15),Random(1,360),128)
		Stop
	}
}
Actor ReckoningRocketLauncherReplacer : RandomSpawner Replaces RocketLauncher
{
	DropItem "RocketLauncherReplacer" 256 100
	DropItem "Q2Phalanx" 256 33
}